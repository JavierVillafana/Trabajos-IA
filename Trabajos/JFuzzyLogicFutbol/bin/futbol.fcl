FUNCTION_BLOCK futbol	//Definicion del Bloque

VAR_INPUT 				//DEFINICION DE VARIABLES DE ENTRADA
	posicion : REAL;
	falta : REAL;
	area : REAL;
END_VAR

VAR_OUTPUT				//DEFINICION DE VARIABLES DE SALIDA
	result : REAL;	
END_VAR

FUZZIFY falta										//FUZZIFICAR VARIABLE DE ENTRADA falta{LEVE,MEDIO,GRAVE}
	TERM leve:=	 	 (0,1) (2.5,0);						
	TERM medio:=	 (2.5,0) (3.75,1) (5,0);				
	TERM fuerte:=	 (5,0) (6.25,1) (7.5,0);			
	TERM muy_fuerte:=(7.5,0) (8.75,1) (10,1);		
END_FUZZIFY

FUZZIFY posicion
	TERM defensa:= 	 (0,1) (2,1) (3,0);						
	TERM delantera:= (3,0) (3.5,1) (6,1) (6.5,0);		
	TERM mediocampo:=(6.5,0) (7,1) (9.5,1) (10,0);			
END_FUZZIFY

FUZZIFY area
	TERM dentro:=	(0,0) (2.5,1) (5,0);					
	TERM fuera:=	(5,0) (7.5,1) (10,0);				
END_FUZZIFY

DEFUZZIFY result		//DEFUZZIFICAR VARIABLE DE SALIDA result{no tarjeta,amarilla,roja}
	TERM notarjeta:= 	 (0,1) (2,1) (3,0);						
	TERM amarilla:= 	 (3,0) (3.5,1) (6,1) (6.5,0);		
	TERM roja:=			 (6.5,0) (7,1) (9.5,1) (10,0);
	METHOD : COG;		 // Use 'Center Of Gravity' defuzzIFication method
	DEFAULT := 0;		 // Default value IS 0 (IF no rule activates defuzzIFier)
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			// Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)
	ACT : MIN;			// Use 'min' activation method
	ACCU : MAX;			// Use 'max' accumulation method

	RULE 1:IF falta IS muy_fuerte THEN result IS roja;
	RULE 2:IF (falta IS leve) AND (posicion IS defensa) THEN result IS notarjeta;
	RULE 3:IF (falta IS medio) AND (posicion IS defensa) THEN result IS notarjeta;
	RULE 4:IF (falta IS fuerte) AND (posicion IS defensa) THEN result IS amarilla;
	
	RULE 5:IF (falta IS leve) AND (posicion IS defensa) AND area IS dentro THEN result IS notarjeta;
	RULE 6:IF (falta IS medio) AND (posicion IS defensa) AND area IS dentro THEN result IS amarilla;
	RULE 7:IF (falta IS fuerte) AND (posicion IS defensa) AND area IS dentro THEN result IS roja;

	RULE 8:IF (falta IS leve) AND (posicion IS defensa) AND area IS fuera THEN result IS notarjeta;
	RULE 9:IF (falta IS medio) AND (posicion IS defensa) AND area IS fuera THEN result IS notarjeta;
	RULE 10:IF (falta IS fuerte) AND (posicion IS defensa) AND area IS fuera THEN result IS amarilla;

	//-mediocampo-
	RULE 11:IF falta IS leve AND posicion IS mediocampo THEN result IS notarjeta;
	RULE 12:IF falta IS medio AND posicion IS mediocampo THEN result IS notarjeta;
	RULE 13:IF falta IS fuerte AND posicion IS mediocampo THEN result IS amarilla;

	//-delantera-
	RULE 14:IF falta IS leve AND posicion IS delantera THEN result IS notarjeta;
	RULE 15:IF falta IS medio AND posicion IS delantera THEN result IS amarilla;
	RULE 16:IF falta IS fuerte AND posicion IS delantera THEN result IS amarilla;

	RULE 17:IF falta IS fuerte AND posicion IS delantera AND area IS dentro THEN result IS roja;

	RULE 18:IF falta IS leve AND posicion IS delantera AND area IS fuera THEN result IS notarjeta;
	RULE 19:IF falta IS medio AND posicion IS delantera AND area IS fuera THEN result IS notarjeta;
	RULE 20:IF falta IS fuerte AND posicion IS delantera AND area IS fuera THEN result IS amarilla;

END_RULEBLOCK

END_FUNCTION_BLOCK

